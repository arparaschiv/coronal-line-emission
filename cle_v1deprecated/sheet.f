C
C PURPOSE: CALCULATES A SIMPLE SHEET FIELD
C
C INPUTS:
C       RAD    RADIUS OF POINT RELATIVE TO SUN CENTER, UNITS OF SOLAR RADII
C       THET   AZIMUTHAL ANGLE, UNITS RADIANS, IN S FRAME
C       PH     POLAR ANGLE, UNITS RADIANS, IN S FRAME
C       IPRINT SET TO 1 FOR MORE VERBOSE OUTPUT
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C       ALSO READS FILE "SHEET.DAT" IN ORDER TO DEFINE WHAT KIND 
C       OF CALCULATION.  HERE IS AN EXAMPLE
C*       
C* sheet.dat
C* give
C* B,   TEMP, VEL, TURBV, SHEETN
C* 
C  1.   1.6e6  0.  20.  1.e8
C*
C* skind- what kind of sheet?
C* put PLASMOID, LAMINAR, RANDOM
CPLASMOID
C* if LAMINAR or RADOM then we are done reading
C* if plasmoid the read number of plasmoids and y z positions and strengths
C5
C1.05 1.09 1.13 1.17 1.21
C0. 0. 0. 0. 0.
C* 1. 1. .3 .3 .1
C1. 1.  1. 1. 1. 
C*
C*end
C* 
C	
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C OUTPUTS:
C       B      MAGNITUDE OF FIELD
C       T      POLAR ANGLE
C       P      AZIMUTHAL ANGLE
C       EDENS  ELECTRON DENSITY /CM3
C       HDENS  HYDROGEN NUCLEI DENSITY /CM3 
C       TEMP   ELECTRON TEMPERATURE K
C       VEL    FLOW VELOCITY ALONG LINE OF SIGHT (+VE MEANS TOWARDS OBSERVER)
C       TURBV  MICROTURBULENCE KM/S (CONTRIBUTES TO LINE PROFILE WIDTHS)
C COMMON:
C
C COMMENTS: AUGUST 24, 2000, P. JUDGE
C
C NOTES:    USER-SUPPLIED ROUTINE
C 
	SUBROUTINE SHEET(RAD,THET,PH,B,T,P,EDENS,HDENS,
     *    TEMP,VEL,TURBV,IPRINT)
	INCLUDE 'PREC'
	PARAMETER(MCEN=10)
	DIMENSION YCEN(MCEN), ZCEN(MCEN), BCEN(MCEN)
	CHARACTER*(4) SKIND
	COMMON /CSHEET/ ICALL,BS, TEMPS, VELS, TURBVS, SHEETNS,
     *                 SKIND,NCEN,YCEN,ZCEN,BCEN
        INCLUDE 'CCONST'
	INCLUDE 'CGRID'
	INCLUDE 'CINPUT'
	INCLUDE 'CLU'
	COMMON /WATM/BX,BY,BZ
	DATA ICALL, ZER, SMALL /0,0.,1.E-20/
C
	IF (ICALL .EQ. 0) THEN 
	   CALL CSTRIP(LDIP,'SHEET.DAT')
	   READ(LDUMS,*) BS, TEMPS, VELS, TURBVS, SHEETNS
	   EDENS=SHEETN
	   READ(LDUMS,*) SKIND
	   CALL LJUST(SKIND)
	   IF(SKIND(1:4) .EQ. 'PLAS') THEN
	      READ(LDUMS,*) NCEN
	      IF(NCEN .GT. MCEN) CALL STOP('NCEN .GT. MCEN') 
	      READ(LDUMS,*)(YCEN(J),J=1,NCEN)
	      READ(LDUMS,*)(ZCEN(J),J=1,NCEN)
	      READ(LDUMS,*)(BCEN(J),J=1,NCEN)
	      write(*,*)zcen
	   ENDIF
	   CALL CLOSE(LDUMS)
	   ICALL=ICALL+1
	ENDIF
C
C	assign variables to saved values
C
	B=BS
	TEMP=TEMPS
	VEL=VELS
	TURBV=TURBVS
	SHEETN=SHEETNS
	EDENS=SHEETN
C       
	IF(SKIND(1:4) .NE. 'PLAS') THEN 
C       
C     X = LOS
C     Y = E-W   ALL IN SOLAR FRAME
C     Z = S-N
C        
C     THET IS ANGLE CLOCKWISE FROM + Z AXIS IN POS
C      PHI IS ANGLE FROM + X AXIS IN THE PLANE PERP TO Z AXIS
C
	   T= -PI/2.+0.01
	   IF(THET .LT. PI/2. .AND. THET .GT. -PI/2)  T= PI/2.-0.01
C
C RANDOM	
C
	   IF(SKIND(1:4) .EQ. 'RAND') T=2.*PI*RAND()
	   P=PI/2.
	ENDIF
C
C       No LOS field for any case
C	
	BX=0.
	BY=B*SIN(T)*SIN(P)
	BZ=B*COS(T)
C 
C  CALCULATE CARTESIAN COORDINATES OF THE POINT RAD,THET,PH
C       AND SET VALUES FAR FROM THE CURRENT SHEET
C	
	X=RAD*SIN(THET)*COS(PH)
	Y=RAD*SIN(THET)*SIN(PH)
	Z=RAD*COS(THET)
C
C  ADD VECTOR POTENTIAL OF A DIPOLAR FIELD UNDER 
C  THE ACTIVE REGION
	RX=X-XD
	RY=Y-YD
	RZ=Z-ZD
C       STRENGTH OF DIPOLE (ARB UNITS)
	DM=.3
C       PROJECTIONS OF DIPOLE ORIENTATION IN X,Y,Z COORD SYSTEM
C       SQUARES MUST ADD UP TO 1
	XM=0.
	YM=1.
	ZM=0.
	AXD=DIPOT(DM,XM,YM,ZM,RX,RY,RZ)

	
C   VECTOR POTENTIAL FOR LINE CURRENT AT YCEN, ZCEN
C       SOLAR RADII
C	
	IF(SKIND(1:4) .EQ. 'PLAS') THEN 
	   AX= B*XLINPOT(Y,Z) +AXD
C
C   CURL A = B	
C       straigh difference only, using a finite difference with denominator
C       0.0003 RSUN
C
	   D=0.0003
	   AXP= B*XLINPOT(Y,Z+D)+AXD
	   BY=  (AXP-AX)/D
C
	   AXP= B*XLINPOT(Y+D,Z)+AXD
	   BZ= -(AXP-AX)/D
C       
	   T=ATAN2(BY,BZ)
	   P=PI/2
	   B=SQRT(BZ*BZ+BY*BY)
	ENDIF
C       
	IF(ABS(X) .GT. 0.1) THEN
	   EDENS=SHEETN/1.e10
	ENDIF
C       
	IF(ABS(Z) .GT. 1.2) THEN
	   EDENS=SHEETN/1.e10
	ENDIF
C       
	IF(Y .GT. 2.5) THEN
	   EDENS=SHEETN/1.e10
	ENDIF
        HDENS=0.8*EDENS
        RETURN
	END
C
C**********************************************************************
C
	FUNCTION XLINPOT(Y,Z)
	INCLUDE 'PREC'
	PARAMETER(MCEN=10)
	DIMENSION YCEN(MCEN), ZCEN(MCEN), BCEN(MCEN)
	CHARACTER*(4) SKIND
	COMMON /CSHEET/ ICALL,BS, TEMPS, VELS, TURBVS, SHEETNS,
     *                 SKIND,NCEN,YCEN,ZCEN,BCEN
C       
C       VECTOR POTENTIAL AX FROM A LINE CURRENT CENTERED
C       YCEN AND ZCEN FLOWING PARALLEL TO THE X AXIS 	
C
	XLINPOT=0.
	N=NCEN
	DO J=1,N
	   YD=Y-YCEN(J)
	   ZD=Z-ZCEN(J)
	   R=SQRT(YD*YD+ZD*ZD)
	   XLINPOT= XLINPOT -BCEN(J)*ALOG(R)
	ENDDO
	RETURN
	END
