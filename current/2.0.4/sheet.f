C
C PURPOSE: CALCULATES SHEET FIELDS
C
C INPUTS:
C       RAD    RADIUS OF POINT RELATIVE TO SUN CENTER, UNITS OF SOLAR RADII
C       THET   AZIMUTHAL ANGLE, UNITS RADIANS, IN S FRAME
C       PH     POLAR ANGLE, UNITS RADIANS, IN S FRAME
C       IPRINT SET TO 1 FOR MORE VERBOSE OUTPUT
C
C
C       ALSO READS FILE "SHEET.DAT" IN ORDER TO DEFINE WHAT KIND 
C       OF CALCULATION.  
C
C OUTPUTS:
C       B      MAGNITUDE OF FIELD
C       T      POLAR ANGLE
C       P      AZIMUTHAL ANGLE
C       EDENS  ELECTRON DENSITY /CM3
C       HDENS  HYDROGEN NUCLEI DENSITY /CM3 
C       TEMP   ELECTRON TEMPERATURE K
C       VEL    FLOW VELOCITY ALONG LINE OF SIGHT (+VE MEANS TOWARDS OBSERVER)
C       TURBV  MICROTURBULENCE KM/S (CONTRIBUTES TO LINE PROFILE WIDTHS)
C COMMON:
C
C COMMENTS: AUGUST 24, 2000, P. JUDGE
C
C NOTES:    USER-SUPPLIED ROUTINE
C 
	SUBROUTINE SHEET(RAD,THET,PH,B,T,P,EDENS,HDENS,
     *    TEMP,VEL,TURBV,IPRINT)
	INCLUDE 'PREC'
	PARAMETER(MCEN=10)
	DIMENSION YCEN(MCEN), ZCEN(MCEN), BCEN(MCEN)
	CHARACTER*(4) SKIND
	COMMON /CSHEET/ ICALL,BS, TEMPS, VELS, TURBVS, SHEETNS,
     *                 SKIND,NCEN,YCEN,ZCEN,BCEN
	COMMON /CDIP/ XD,YD,ZD,XM,YM,ZM,DM,DELTM
        INCLUDE 'CCONST'
	INCLUDE 'CGRID'
	INCLUDE 'CINPUT'
	INCLUDE 'CLU'
	COMMON /WATM/BX,BY,BZ
	DATA ICALL, ZER, SMALL /0,0.,1.E-20/
C
C 
C  CALCULATE CARTESIAN COORDINATES OF THE POINT RAD,THET,PH
C       AND SET VALUES FAR FROM THE CURRENT SHEET
C	
	X=RAD*SIN(THET)*COS(PH)
	Y=RAD*SIN(THET)*SIN(PH)
	Z=RAD*COS(THET)
C
C       READ INPUT ON FIRST CALL
C	
	IF (ICALL .EQ. 0) THEN 
	   CALL CSTRIP(LDIP,'SHEET.DAT')
	   READ(LDUMS,*)XD,YD,ZD
	   READ(LDUMS,*)XM,YM,ZM
	   READ(LDUMS,*)DM,DELTM
	   READ(LDUMS,*) BS, TEMPS, VELS, TURBVS, SHEETNS
	   EDENS=SHEETN
	   READ(LDUMS,*) SKIND


	   WRITE(LOUT,*)' '
	   WRITE(LOUT,*)' '
	   WRITE(LOUT,*)'1 SHEET PARAMETERS'
	   WRITE(LOUT,700)'DIPOLE POSITION  X,Y,Z        ',XD,YD,ZD
	   WRITE(LOUT,700)'DIPOLE PROJECTIONS ON TO X,Y,Z',XM,YM,ZM
	   WRITE(LOUT,701)'DIPOLE MOMENT DELTM ',DM, DELTM
	   WRITE(LOUT,702) 'B, T, V, TURB, NE ',BS, TEMPS, VELS, TURBVS, SHEETNS
	   WRITE(LOUT,*) SKIND
	   WRITE(LOUT,*)' '
	   WRITE(LOUT,*)' '
 700	   FORMAT(1X,A30,1P,3(1X,E9.2),0P)
 701	   FORMAT(1X,A30,1P,2(1X,E9.2),0P)
 702	   FORMAT(1X,A30,1P,5(1X,E9.2),0P)
C

	   CALL LJUST(SKIND)
	   IF(SKIND(1:4) .EQ. 'PLAS') THEN
	      READ(LDUMS,*) NCEN
	      WRITE(lout,711) 'NUMBER OF LINE CURRENTS',NCEN
	      IF(NCEN .GT. MCEN) CALL STOP('NCEN .GT. MCEN') 
	      READ(LDUMS,*)(YCEN(J),J=1,NCEN)
	      WRITE(LOUT,710) 'Y',(YCEN(J),J=1,NCEN)
	      READ(LDUMS,*)(ZCEN(J),J=1,NCEN)
	      WRITE(LOUT,710) 'Z',(ZCEN(J),J=1,NCEN)
	      READ(LDUMS,*)(BCEN(J),J=1,NCEN)
	      WRITE(LOUT,710) 'B',(BCEN(J),J=1,NCEN)
 710	      FORMAT(A30,1X,1P,8(E9.1,1X),0P)
 711	      FORMAT(A30,1X,I4)
	   ENDIF
	   CALL CLOSE(LDUMS)
	   ICALL=ICALL+1
	ENDIF
C
C	ASSIGN VARIABLES TO SAVED VALUES
C
	B=BS
	TEMP=TEMPS
	VEL=VELS
	TURBV=TURBVS
	SHEETN=SHEETNS
	EDENS=SHEETN
C
C  VECTOR POTENTIAL CALCULATION 
C  ADD VECTOR POTENTIAL OF A DIPOLAR FIELD
C
	RX=X-XD
	RY=Y-YD
	RZ=Z-ZD
C
C   VECTOR POTENTIAL FOR LINE CURRENTS AT YCEN, ZCEN
C       SOLAR RADII
C	
	AXD=DIPOT(DM,XM,YM,ZM,RX,RY,RZ)
	IF(SKIND(1:4) .EQ. 'PLAS') THEN
C
C TOTAL VECTOR POTENTIAL
	   AX= B*XLINPOT(Y,Z) + AXD
C
C
C   CURL A = B: FINITE DIFFERENCE WITH DENOMINATOR DELTM IN RSUN
C
	   D=DELTM
	   BX=0.  
	   AXP=  B*XLINPOT(Y,Z+D) + DIPOT(DM,XM,YM,ZM,RX,RY,RZ+D)
	   BY =  (AXP-AX)/D
	   AXP = B*XLINPOT(Y+D,Z) + DIPOT(DM,XM,YM,ZM,RX,RY+D,RZ)
	   BZ= -(AXP-AX)/D
	   T=ATAN2(BY,BZ)
	   P=PI/2.
	   B=SQRT(BZ*BZ+BY*BY+BX*BX)
	ELSE
C
C LAMINAR OR RANDOM CASES
C
	   B=BS
	   IF(SKIND(1:4) .EQ. 'LAMI') THEN 
	      T= -PI/2.+0.001
	      IF(THET .LT. PI/2. .AND. THET .GT. -PI/2)  T= PI/2.-0.001
	      P=PI/2.
	   ENDIF
	   IF(SKIND(1:4) .EQ. 'RAND') THEN
	      T=2.*PI*RAND()
	      P=2.*PI*RAND()
	   ENDIF
C
C FOR OUTPUT
C	
	   BX=B*COS(T)*SIN(P)
	   BY=B*SIN(T)*SIN(P)
	   BZ=B*COS(T)
	ENDIF
	
C	IF(ABS(X) .GT. 0.04) THEN
C	   EDENS=SHEETN/1.e20
C	ENDIF
C       
	IF(ABS(Z) .GT. 0.3) THEN
	   EDENS=SHEETN/1.e20
	ENDIF
C       
	IF(Y .GT. 2.5) THEN
	   EDENS=SHEETN/1.e20
	ENDIF
        HDENS=0.8*EDENS
        RETURN
	END
C
C**********************************************************************
C
	FUNCTION XLINPOT(Y,Z)
	INCLUDE 'PREC'
	PARAMETER(MCEN=10)
	DIMENSION YCEN(MCEN), ZCEN(MCEN), BCEN(MCEN)
	CHARACTER*(4) SKIND
	COMMON /CSHEET/ ICALL,BS, TEMPS, VELS, TURBVS, SHEETNS,
     *                 SKIND,NCEN,YCEN,ZCEN,BCEN
C       
C       VECTOR POTENTIAL AX FROM A LINE CURRENT CENTERED
C       YCEN AND ZCEN FLOWING PARALLEL TO THE X AXIS 	
C
	XLINPOT=0.
	N=NCEN
	DO J=1,N
	   YD=Y-YCEN(J)
	   ZD=Z-ZCEN(J)
	   R=SQRT(YD*YD+ZD*ZD)
	   XLINPOT= XLINPOT -BCEN(J)*DLOG(R)
	ENDDO
	RETURN
	END










