C
C PURPOSE: CALCULATE EMISSION COEFFICIENTS FOR STOKES VECTOR 
C
C INPUTS:
C       KR   TRANSITION INDEX (1=FIRST TRANSITION, ETC)
C       T0   GEOMETRIC TENSOR (TABLE 1 OF
C            CASINI, R. & JUDGE, P. G., 1999. AP J 522, 524-539)
C OUTPUTS:
C   EMISS(KR,I,NY)  EMISSION COEFFICIENT IN PH/CM2/S/SR/ANGSTROM 
C   WHERE  KR IS THE LINE INDEX
C          I IS THE STOKES INDEX (0=INTENSITY, 1=Q, ETC.)
C          NU IS THE FREQUENCY INDEX
C
C COMMON:
C       CSE CSLINE CATOM CORON SGNM CLU
C COMMENTS: OCTOBER 6, 1999, P. JUDGE
C 
C       4TH NOVEMBER 2019
C	ADDED WHITE LIGHT EMISSION TO EMISSIVITY
C	
	SUBROUTINE EMISSION(KR,T0)
	INCLUDE 'PREC'
	INCLUDE 'PARAM'
	INCLUDE 'CSE'
	INCLUDE 'CSLINE'
	INCLUDE 'CCONST'
	INCLUDE 'CATOM'
	INCLUDE 'CORON'
	INCLUDE 'SGNM'
	INCLUDE 'CINTS'
	INCLUDE 'CLU'
C
	PARAMETER (SQRT2=.14142135623730950488E1)
	PARAMETER (SQRT3=.17320508075688772935E1)
C
	DIMENSION T0(0:3,0:2)
	DIMENSION EM_FACT(0:4)
C
	IJ0=IRAD(KR)
	IJ1=JRAD(KR)
	RJ0=AJ(IJ0)
	RJ1=AJ(IJ1)
C
C CALCULATE C_COEFF
C
	HNU=HH*CC / (ALAMB(KR)/1.E8)
	C_COEFF=TOTN*SQRT(TWO*RJ1+ONE)*RHO(IJ1,0)*A(KR)
     *         *HNU/FOUR/PI
C
	G0=GLAND(IJ0)
	G1=GLAND(IJ1)
	GEFF=0.5*(G0+G1)+0.25*(G0-G1)*(RJ0*(RJ0+ONE)-RJ1*(RJ1+ONE))
	SIGMA=RHO(IJ1,2)/RHO(IJ1,0)
C
C   CALCULATE DCOEFF AND ECOEFF
C
	D_COEFF=-SG(NINT(RJ0+RJ1))*SQRT(THREE*(TWO*RJ1+1))
     /	 *FUN6J(ONE,ONE,TWO,RJ1,RJ1,RJ0)
C
C      E_COEFF=THREE*SQRT(TWO*RJ1+ONE)
C     /	 *(SG(NINT(RJ1-RJ0))*G1*SQRT(RJ1*(RJ1+ONE)*(TWO*RJ1+ONE))
C    /	 *FUN6J(ONE,TWO,ONE,RJ1,RJ1,RJ1)
C     /	 *FUN6J(ONE,ONE,ONE,RJ1,RJ1,RJ0)
C     /	 -G0*SQRT(RJ0*(RJ0+ONE)*(TWO*RJ0+ONE))
C     /	 *FUN9J(ONE,TWO,ONE,RJ0,RJ1,ONE,RJ0,RJ1,ONE))
c
c check	
c
c       alternate calculation of E given D and geff
c	
	F_COEFF=3./4. * (RJ1*(RJ1+ONE) - RJ0*(RJ0+ONE) -2.)*(G1-G0)
	E_COEFF=D_COEFF/SQRT2*(F_COEFF+GEFF)
C	IF(ABS(F_COEFF) .GT. 1.E-16) WRITE(*,923) 'D&D',IJ1, IJ0, G1,G0,GEFF,
C     *        D_COEFF, E_COEFF, F_COEFF
C 923	FORMAT(A,2(I2,1X),5(1X,F6.3),1X,E9.2)

C
C   CALCULATE EMISSION COEFFICIENTS
C
	EM_FACT(0)=ONE+D_COEFF*SIGMA*T0(0,2)
	EM_FACT(1)=D_COEFF*SIGMA*T0(1,2)
	EM_FACT(2)=D_COEFF*SIGMA*T0(2,2)
	EM_FACT(3)=-(SQRT2/SQRT3)*ALARMOR*(GEFF+E_COEFF*SIGMA)*T0(3,1) 
	EM_FACT(4)=-(SQRT2/SQRT3)*ALARMOR*GEFF*T0(3,1) 
C
C EMISS(*,4,*): MAGNETOGRAM STOKES V
C
	SCL=C_COEFF*1.E-13*ALAMB(KR)
C	SI=0.
C	SQ=0.
C	SU=0.
C       
C       4TH NOVEMBER 2019
C	CALL KCOR(KR, SI, SQ, SU)
C       4TH NOVEMBER 2019
	
C	DO NY=1,NQ(KR)
C	  EMISS(KR,0,NY)=EM_FACT(0)*PHI(KR,NY)*C_COEFF + SI
C	  EMISS(KR,1,NY)=EM_FACT(1)*PHI(KR,NY)*C_COEFF + SQ
C	  EMISS(KR,2,NY)=EM_FACT(2)*PHI(KR,NY)*C_COEFF + SU
C	  EMISS(KR,3,NY)=-EM_FACT(3)*PHIP(KR,NY)*SCL
C	  EMISS(KR,4,NY)=-EM_FACT(4)*PHIP(KR,NY)*SCL
C	END DO
	DO NY=1,NQ(KR)
	  EMISS(KR,0,NY)=EM_FACT(0)*PHI(KR,NY)*C_COEFF 
	  EMISS(KR,1,NY)=EM_FACT(1)*PHI(KR,NY)*C_COEFF 
	  EMISS(KR,2,NY)=EM_FACT(2)*PHI(KR,NY)*C_COEFF 
	  EMISS(KR,3,NY)=-EM_FACT(3)*PHIP(KR,NY)*SCL
	  EMISS(KR,4,NY)=-EM_FACT(4)*PHIP(KR,NY)*SCL
	END DO
	RETURN
	END
C
C	
C
	SUBROUTINE KCOR(KR,EI,EQ,EU)
	INCLUDE 'PREC'
	INCLUDE 'PARAM'
	INCLUDE 'CSE'
	include 'CGRID'
	INCLUDE 'CSLINE'
	INCLUDE 'CCONST'
	INCLUDE 'CATMOS'
	INCLUDE 'CATOM'
	INCLUDE 'CORON'
	INCLUDE 'SGNM'
	INCLUDE 'CINTS'
	INCLUDE 'CLU'
C
C NEW ROUTINE OCTOBER 2019 PGJ 
C
C RETURNS K-CORONAL EMISSION COEFFICIENTS 
C
	RRR=SQRT(GX*GX+GY*GY+GZ*GZ)
	ROTANG=ATAN(GZ,GY)
	SGG=1./RRR
	CGG=SQRT(1.-SGG*SGG)
	QQ=ZERO
C	write(*,*) "RRR, ROTANG, SGG, CGG, QQ"
C	write(*,*) RRR, ROTANG, SGG, CGG, QQ
C       
C   EQUATIONS (11) AND (12) OF VANDEHULST 1950
C  
        TAPB = (1.-QQ)/(1.-QQ/3.) * 2.*(1.-CGG) + 
     *           QQ/(1.-QQ/3.)      * (1.-CGG*CGG/SGG * LOG((1.+SGG)/CGG) )

	TAMB = (1.-QQ)/(1.-QQ/3.) *  2./3.*(1.-CGG**3) + 
     *       QQ/(1.-QQ/3.)       *  .25* (1.+SGG*SGG - CGG**4/SGG * 
     *                                 LOG((1.+SGG)/CGG) )

	AAA=(TAPB+TAMB)/2.
	BBB=(TAPB-TAMB)/2.
C    
C EQUATION (13) VDH 1950
C
	SIGMAT=6.6524587E-25   
	FF=CC*1.E8/ALAMB(KR)
	TR=5900.
	BETA=HH*FF/BK/TR
	B0= 2.*HH*FF * (FF/CC)**2. / (EXP(BETA) -1.)
C
C       WE NEED INTENSITY PER ANGSTROM FOR OUTPUT
C
	WWW=ALAMB(KR)
	B0 = B0 * CC*1.E8/WWW/WWW
C	
	ST=GY/SQRT(GY*GY+GX*GX)   
	CT=SQRT(1.-ST*ST)
C
C EQUATIONS (14) AND (15) VDH WHICH GIVE (16) AND (17) INTEGRANDS
C
	TTK=3./8. * B0 * SIGMAT * AAA * ED
	RRK=3./8. * B0 * SIGMAT * (AAA * CT*CT + BBB *ST*ST) *ED
	EI=TTK+RRK
	EQQ=TTK-RRK
	EUU=0.
C	WRITE(*,*)"B0,TTK,RRK, ED", B0,TTK,RRK, ED

C       
C POS ROTATION 	
C
	ROTANG=-2*ROTANG
	EQ=EQQ*COS(ROTANG) - EUU*SIN(ROTANG)
	EU=EQQ*SIN(ROTANG) + EUU*COS(ROTANG)


	RETURN
	END
	
	
